%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

role mobile_node (MN,MAG,AuS : agent, X,Y : symmetric_key, H : hash_func, SND,RCV : channel(dy))
	
	played_by MN

	def=

		local	
			State, T1, T2, T3, T4, V, W : nat,
			HK : message,
			AuthRqst, AuthResp, RtrSol, RtrAdv : message,
			MIC1, MIC2, MIC3, MIC4 : message

		init	
			State := 0

		transition

			0.	State	= 0 /\ RCV(start) =|>
				State' := 1 /\ T1':= new() 
							/\ HK':= xor(H(X),H(Y))
							/\ MIC1':=H(MN.T1'.HK')
							/\ SND(AuthRqst.MN.T1'.MIC1') 
							/\ secret(X,sec_x,{MN,AuS}) 
							/\ secret(Y,sec_y,{MN,AuS}) 
							/\ secret(HK',sec_hk,{MN,AuS})
							/\ witness(MN,AuS,auth_aus_mn,HK')

			1.	State	= 1 /\ RCV(AuthResp.MN.W'.T2'.MIC2') /\ MIC2'=H(MN.W'.T2'.HK) =|>
				State' := 2 /\ V':= xor(W',H(HK))
							/\ T3':= new() 
							/\ MIC3':=H(MN.T3'.V')
							/\ SND(RtrSol.MN.T3'.MIC3')
							/\ request(MN,AuS,auth_mn_aus,HK) 
							/\ witness(MN,MAG,auth_mag_mn,V')

			2.	State	= 2 /\ RCV(RtrSol.MN.T4'.MIC4') /\ MIC4'=H(MN.T4'.V) =|>
				State' := 3 /\ request(MN,MAG,auth_mn_mag,V)

end role

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

role media_access_gateway (MN,MAG,AuS : agent, SK : symmetric_key, H : hash_func, SND,RCV : channel(dy))
			
	played_by MAG

	def= 
	
		local 	
			State, T2, T3, T4, V, W : nat,
			AuthResp, RtrSol, AuthSucc, RtrAdv: message,
			MIC2, MIC3, MIC4 : message

		init
			State  := 0

		transition

			0.	State	= 0 /\ RCV(AuthResp.MN.{V'}_SK.W'.T2'.MIC2') =|>
				State' := 1 /\ SND(AuthResp.MN.W'.T2'.MIC2')

			1.	State	= 1 /\ RCV(RtrSol.MN.T3'.MIC3') /\ MIC3'=H(MN.T3'.V) =|>
				State' := 2 /\ T4':= new()
							/\ MIC4':=H(MN.T4'.V)
							/\ SND(AuthSucc.MN)
							/\ SND(RtrAdv.MN.T4'.MIC4')
							/\ request(MAG,MN,auth_mag_mn,V)
							/\ witness(MAG,MN,auth_mn_mag,V)

end role

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

role authentication_server (MN,MAG,AuS : agent, X,Y,SK : symmetric_key, H : hash_func, SND,RCV : channel(dy))
			
	played_by AuS 

	def=

		local		
			State, T1, T2, V, W : nat,
			HK : message,
			AuthRqst, AuthResp, AuthSucc : message,
			MIC1, MIC2 : message
			
		init	
			State := 0

		transition

			0.	State	= 0 /\ RCV(AuthRqst.MN.T1'.MIC1') /\ MIC1'=H(MN.T1'.HK') =|>					
				State' := 1 /\ HK':=xor(H(X),H(Y))
							/\ V' := new() 
							/\ W':= xor(V',H(HK'))
							/\ T2':= new()
							/\ MIC2':=H(MN.W'.T2'.HK')
							/\ SND(AuthResp.MN.{V'}_SK.W'.T2'.MIC2')
							/\ secret(X,sec_x,{MN,AuS}) 
							/\ secret(Y,sec_y,{MN,AuS}) 
							/\ secret(HK',sec_hk,{MN,AuS}) 
							/\ request(AuS,MN,auth_aus_mn,HK') 
							/\ witness(AuS,MN,auth_mn_aus,HK')

			1.	State	= 1 /\ RCV(AuthSucc.MN) =|>
				State' := 2
				
end role	

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

role session (MN,MAG,AuS : agent, X,Y,SK : symmetric_key, H : hash_func, SND,RCV : channel(dy))

	def=
		composition	
			mobile_node(MN,MAG,AuS,X,Y,H,SND,RCV)
			/\
			media_access_gateway(MN,MAG,AuS,SK,H,SND,RCV)
			/\
			authentication_server(MN,MAG,AuS,X,Y,SK,H,SND,RCV)
 
end role

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

role enviroment() 

	def=

		const	
			mn,mag,aus : agent,
			h : hash_func,
			x,y,sk : symmetric_key,
			snd,rcv	: channel(dy),
			
			sec_x, sec_y, sec_hk: protocol_id,
			auth_aus_mn, auth_mn_aus, auth_mag_mn, auth_mn_mag : protocol_id

		intruder_knowledge = {mn,mag,aus,h,i}

		composition
			session(mn,mag,aus,x,y,sk,h,snd,rcv)

end role

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

goal
	secrecy_of sec_x
	secrecy_of sec_y
	secrecy_of sec_hk

	authentication_on auth_aus_mn
	authentication_on auth_mn_aus
	authentication_on auth_mag_mn
	authentication_on auth_mn_mag

end goal

enviroment()

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
