%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

role client (	C,NAS	: agent,
		R,J 	: symmetric_key,
		H 	: hash_func,
		SND,RCV : channel(dy) 	)

played_by C

def= 

	local	
		State			: nat,
		D1s,D2,D3,D4,Ask	: message,
		Hk,W,V			: message,
		T1,T2,T3,T4		: text,	
		SessionKeyEstablishment	: message

	init	State	:= 0

transition

	0.	State	= 0	/\ RCV(start)				=|>
		State' := 1	/\ Hk':= xor(H(R),H(J))
				/\ T1':= new()		    	
				/\ D1s':= H(C.T1'.Hk')				
				/\ SND(C.T1'.D1s')				

	1.	State   = 1 	/\ RCV(W'.T2'.D2') /\ D2' = H(W'.T2'.H(xor(H(R),H(J))))	=|>
	%1.	State	= 1	/\ RCV(W')				=|>
		State' := 7	/\ V':= xor(W',H(Hk))
				/\ SND(Ask)
				/\ request(C,NAS,c_nas_v,V)

	7.	State	= 7	/\ RCV(T3'.D3') /\ D3'= H(T3'.V) 	=|>
		State' := 9	/\ T4' := new()
				/\ D4' := H(T4'.V)
				/\ SND(T4'.D4')				

	9.	State	= 9	/\ RCV(SessionKeyEstablishment)		=|> 
		State' := 11 	/\ witness(C,NAS,nas_c_v,V)

end role

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

role network_access_server (	C,NAS		: agent,
				AuS_NAS_SK	: symmetric_key,
				H		: hash_func,
				SND,RCV 	: channel(dy))
			
played_by NAS

def= 

	local 	
		State			: nat,
		Ack,Ask,D3,D4		: message,
		V			: message,
		T3,T4			: text,		
		SessionKeyEstablishment	: message

	init	State  := 4

transition

	4.	State   = 4	/\ RCV({V'}_inv(AuS_NAS_SK))		=|>
		State' := 5	/\ SND(Ack)

	5.	State   = 5	/\ RCV(Ask')				=|>
		State' := 8 	/\ T3' := new()
				/\ D3':= H(T3'.V)
				/\ SND(T3'.D3')		
				/\ witness(NAS,C,c_nas_v,V) 
				%/\ request(NAS,C,nas_c_v,V)

	7.	State   = 8	/\ RCV(T4'.D4') /\ D4' = H(T4'.V)	=|>
		State'	:= 10	/\ SND(SessionKeyEstablishment)	
				
end role

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

role authentication_server (	AuS		: agent,
				KeyMap		: agent.symmetric_key.symmetric_key set,
				AuS_NAS_SK	: symmetric_key,
		              	H		: hash_func,
				SND,RCV		: channel(dy))
			
played_by AuS 

def= 
	local		
		State		: nat,
		D1r,D2,Ack	: message,
		T1,T2		: text,
		%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%		
		C		: agent,
		R,J		: symmetric_key,
		Chk,W,V		: message
		
	init	State	:= 2

transition

	2.	State	= 2	/\ RCV(C'.T1'.D1r') /\ in(C'.R'.J',KeyMap) /\ D1r' = H(C'.T1'.xor(H(R'),H(J')))	=|>						
		State'	:= 3	/\ Chk' := xor(H(R'),H(J'))				
				/\ V' := new()				
				/\ SND({V'}_inv(AuS_NAS_SK))				
				/\ secret(R,r_secrecy,{C,AuS}) /\ secret(J,j_secrecy,{C,AuS})

	3. 	State   = 3	/\ RCV(Ack)									=|>
		State' := 6	/\ W' := xor(V,H(Chk))
				/\ T2' := new()
				/\ D2' := H(T2'.Chk) 
				/\ SND(W'.T2'.D2')
				%/\ SND(W')

end role	

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

role session (	C,NAS 		: agent,
		R,J  		: symmetric_key,
		AuS_NAS_SK	: symmetric_key,
		H 		: hash_func,
		SND,RCV 	: channel(dy))

def=
	composition		
		network_access_server(C,NAS,AuS_NAS_SK,H,SND,RCV)
		/\
		client(C,NAS,R,J,H,SND,RCV)
		
end role

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

role enviroment() def=

const	aus,nas,c,i		: agent,
	h			: hash_func,
	rc,jc,ri,ji,aus_nas_sk	: symmetric_key,
	snd, rcv		: channel(dy),
	
	r_secrecy, j_secrecy,
	nas_c_v, 
	c_nas_v			: protocol_id

intruder_knowledge = {aus,nas,c,i,h,ri,ji}

composition
	authentication_server(aus,{c.rc.jc,i.ri.ji},aus_nas_sk,h,snd,rcv)
	/\
	session(c,nas,rc,jc,aus_nas_sk,h,snd,rcv)
	%/\
	%session(i,nas,ri,ji,aus_nas_sk,h,snd,rcv)

end role

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

goal
	secrecy_of r_secrecy, j_secrecy
	authentication_on c_nas_v	
	authentication_on nas_c_v
	
end goal

enviroment()

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
